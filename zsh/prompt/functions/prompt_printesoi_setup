function prompt_printesoi_precmd {
  # Check for untracked files or updated submodules since vcs_info does not.
  if [[ -n $(git ls-files --other --exclude-standard 2> /dev/null) ]]; then
    branch_format="${_PROMPT_PRINTESOI_SEPARATOR} (${_prompt_printesoi_colors[1]}%b%f%u%c${_prompt_printesoi_colors[4]}•%f)"
  else
    branch_format="${_PROMPT_PRINTESOI_SEPARATOR} (${_prompt_printesoi_colors[1]}%b%f%u%c)"
  fi

  zstyle ':vcs_info:*:prompt:*' formats "${branch_format}"

  vcs_info 'prompt'
}

function prompt_printesoi_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent sp subst)

  # Load required functions.
  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  # Add hook for calling vcs_info before each command.
  add-zsh-hook precmd prompt_printesoi_precmd

  : ${_PROMPT_PRINTESOI_SEPARATOR:='->'}
  _prompt_printesoi_colors=(
    "%F{cyan}"
    "%F{yellow}"
    "%F{magenta}"
    "%F{red}"
    "%F{green}"
    "%B%F{white}"
    "%B%F{blue}"
    "%B%F{red}"
    "%F{blue}"
  )

  # Formats:
  #   %b - branchname
  #   %u - unstagedstr (see below)
  #   %c - stagedstr (see below)
  #   %a - action (e.g. rebase-i)
  #   %R - repository path
  #   %S - path in the repository
  local branch_format="(${_prompt_printesoi_colors[1]}%b%f%u%c)"
  local action_format="(${_prompt_printesoi_colors[5]}%a%f)"
  local unstaged_format="${_prompt_printesoi_colors[2]}•%f"
  local staged_format="${_prompt_printesoi_colors[5]}•%f"

  # Set vcs_info parameters.
  zstyle ':vcs_info:*' enable git svn
  zstyle ':vcs_info:*:prompt:*' check-for-changes true
  zstyle ':vcs_info:*:prompt:*' unstagedstr "${unstaged_format}"
  zstyle ':vcs_info:*:prompt:*' stagedstr "${staged_format}"
  zstyle ':vcs_info:*:prompt:*' actionformats "${branch_format}${action_format}"
  zstyle ':vcs_info:*:prompt:*' formats "${branch_format}"
  zstyle ':vcs_info:*:prompt:*' nvcsformats   ""

  # Define prompts.
  PROMPT="
%(?.%B%F{blue}.%B%F{red})λ%?%f%b :: %(!.%F{red}.%F{magenta})%n%f ${_PROMPT_PRINTESOI_SEPARATOR} %F{${(q)_PROMPT_PRINTESOI_HOSTNAME_COLOR:-yellow}}%M%f ${_PROMPT_PRINTESOI_SEPARATOR} ${_prompt_printesoi_colors[5]}%~%f ${_PROMPT_PRINTESOI_SEPARATOR} ${_prompt_printesoi_colors[9]}%*%f "'${vcs_info_msg_0_}'"
%(!.%B%F{red}#.%b%f)${_PROMPT_PRINTESOI_SEPARATOR}%f%b "
  RPROMPT=''
}

prompt_printesoi_setup "$@"
